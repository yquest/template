plugins {
  id 'org.jetbrains.kotlin.jvm' version '1.3.20' apply false
  id 'com.github.johnrengelman.shadow' version '5.0.0' apply false
}

repositories {
  mavenCentral()
  jcenter()
}


allprojects {
  group = 'pt.fabm'
  version = '1.0.0-SNAPSHOT'
}


subprojects {
  if (['rest','shared','memory-dao', 'main'].contains(it.name)) {
    apply plugin: 'org.jetbrains.kotlin.jvm'

    repositories {
      mavenCentral()
      jcenter()
    }

    task copyIntellijMainResources(type: Copy) {
      from "$buildDir/resources/main"
      into "out/production/classes"
    }

    task copyIntellijTestResources(type: Copy) {
      from "$buildDir/resources/test"
      into "out/test/classes"
    }
    
    compileKotlin {
      kotlinOptions.jvmTarget = '1.8'
    }

    compileTestKotlin {
      kotlinOptions.jvmTarget = '1.8'
    }

    ext {
      kotlinVersion = '1.3.20'
      vertxVersion = '3.7.0'
      junitJupiterEngineVersion = '5.4.0'
    }

    test {
      useJUnitPlatform()
      testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
      }
    }
  }
}


task cleanPublic(type: Delete) {
  delete 'web/rest/src/main/resources/public'
  followSymlinks = true
}

task copyPublic(type: Copy) {
  from "frontend/dist"
  into "web/rest/src/main/resources/public"
}

task compileAndCopy(dependsOn: [':frontend:build', 'copyPublic'])
